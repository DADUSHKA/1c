    ВЫБРАТЬ
	ОстаткиТоваровОстаткиИОбороты.Номенклатура КАК Номенклатура,
	ОстаткиТоваровОстаткиИОбороты.Период КАК Период,
	ОстаткиТоваровОстаткиИОбороты.КоличествоКонечныйОстаток КАК КоличествоКонечныйОстаток
ПОМЕСТИТЬ ОстаткиТоваров
ИЗ
	РегистрНакопления.ОстаткиТоваров.ОстаткиИОбороты(&Начало, &Окончание, День, ДвиженияИГраницыПериода, ) КАК ОстаткиТоваровОстаткиИОбороты
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ОстаткиТоваров.Номенклатура КАК Номенклатура,
	ОстаткиТоваров.Период КАК Период
ПОМЕСТИТЬ НулевыеОстатки
ИЗ
	ОстаткиТоваров КАК ОстаткиТоваров
ГДЕ
	ОстаткиТоваров.КоличествоКонечныйОстаток = 0
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	НулевыеОстатки.Номенклатура КАК Номенклатура,
	ВЫБОР
		КОГДА НулевыеОстатки.Период = &Начало
			ТОГДА НулевыеОстатки.Период
		ИНАЧЕ ДОБАВИТЬКДАТЕ(НулевыеОстатки.Период, ДЕНЬ, 1)
	КОНЕЦ КАК ПериодНачало,
	МИНИМУМ(ВЫБОР
			КОГДА ОстаткиТоваров.Период = &Окончание
				ТОГДА ОстаткиТоваров.Период
			ИНАЧЕ ДОБАВИТЬКДАТЕ(ОстаткиТоваров.Период, СЕКУНДА, -1)
		КОНЕЦ) КАК ПериодОкончание
ПОМЕСТИТЬ НулевыеПериоды
ИЗ
	ОстаткиТоваров КАК ОстаткиТоваров
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НулевыеОстатки КАК НулевыеОстатки
		ПО ОстаткиТоваров.Номенклатура = НулевыеОстатки.Номенклатура
			И ОстаткиТоваров.Период > НулевыеОстатки.Период

СГРУППИРОВАТЬ ПО
	НулевыеОстатки.Номенклатура,
	ВЫБОР
		КОГДА НулевыеОстатки.Период = &Начало
			ТОГДА НулевыеОстатки.Период
		ИНАЧЕ ДОБАВИТЬКДАТЕ(НулевыеОстатки.Период, ДЕНЬ, 1)
	КОНЕЦ
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	НулевыеПериоды.Номенклатура КАК Номенклатура,
	НулевыеПериоды.ПериодНачало КАК ПериодНачало,
	НулевыеПериоды.ПериодОкончание КАК ПериодОкончание,
	СУММА(ГрафикиРаботы.Значение) КАК Значение
ИЗ
	НулевыеПериоды КАК НулевыеПериоды
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикиРаботы КАК ГрафикиРаботы
		ПО (ГрафикиРаботы.Дата МЕЖДУ НулевыеПериоды.ПериодНачало И НулевыеПериоды.ПериодОкончание)
ГДЕ
	ГрафикиРаботы.ТипГрафика = ЗНАЧЕНИЕ(Справочник.ТипГрафика.ПустаяСсылка)

СГРУППИРОВАТЬ ПО
	НулевыеПериоды.Номенклатура,
	НулевыеПериоды.ПериодНачало,
	НулевыеПериоды.ПериодОкончание